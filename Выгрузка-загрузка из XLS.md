# Обычные формы

Процедура ВыгрузитьНажатие(Кнопка)
	
	Если ПустаяСтрока(ИмяФайла) Тогда 
		
		ОбщегоНазначения.СообщитьИнформациюПользователю("Не указано имя файла, для сохранения!");
		Возврат;
		
	КонецЕсли;	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Хозрасчетный.КодБыстрогоВыбора,
		|	Хозрасчетный.НаименованиеАнгл
		|ИЗ
		|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
		|ГДЕ
		|	Хозрасчетный.НаименованиеАнгл <> """"";
	
	ТаблицаСчетов = Запрос.Выполнить().Выгрузить();
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	Построитель       = Новый ПостроительОтчета;
	Построитель.ИсточникДанных = Новый ОписаниеИсточникаДанных(ТаблицаСчетов);       
	Построитель.Вывести(ТабличныйДокумент);
	
	ИмяКаталога = "";
	
	Если НЕ РаботаСФайлами.ВыбратьКаталог(ИмяКаталога) Тогда
		
		ОбщегоНазначения.СообщитьИнформациюПользователю("Не выбран каталог, обработка прекращена!");
		Возврат;
		
	КонецЕсли;
	
	ПолноеИмяФайла = РаботаСФайлами.ПолучитьИмяФайла(ИмяКаталога, ИмяФайла + ".xls");
	
	ТабличныйДокумент.Записать(ПолноеИмяФайла, ТипФайлаТабличногоДокумента.XLS97);
	
	ОбщегоНазначения.СообщитьИнформациюПользователю("Файл успешно сохранен!
													|проверьте каталог: " + ИмяКаталога);
	
КонецПроцедуры

Процедура ЗагрузитьНажатие(Элемент)
	
	ДиалогОткрытияФайла = РаботаСФайлами.ПолучитьДиалогВыбораФайлов(Ложь);
	
	Если НЕ ДиалогОткрытияФайла.Выбрать() Тогда
		
		ОбщегоНазначения.СообщитьИнформациюПользователю("Не выбран файл, обработка прекращена!");
		Возврат;
		
	КонецЕсли;
			
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	Попытка
		ТабличныйДокумент.Прочитать(ДиалогОткрытияФайла.ПолноеИмяФайла);
	Исключение
		
		ОбщегоНазначения.СообщитьИнформациюПользователю("Не получилось прочитать файл, по причине: " + ОписаниеОшибки());
		Возврат;
		
	КонецПопытки;
	
	Построитель = Новый ПостроительЗапроса;
	Построитель.ИсточникДанных          = Новый ОписаниеИсточникаДанных(ТабличныйДокумент.Область());
	Построитель.ДобавлениеПредставлений = ТипДобавленияПредставлений.НеДобавлять;
	Построитель.ЗаполнитьНастройки();
	Построитель.Выполнить();
	
	Попытка
		ТаблицаСчетов = Построитель.Результат.Выгрузить();
	Исключение
		//При выводе в таблицу значений через построитель запроса, 
		//названия колонок в источнике данных должны быть первой строкой, 
		//иначе построитель не сможет получить и преобразовать их в колонки таблицы значений и выдаст ошибку.
		ОбщегоНазначения.СообщитьИнформациюПользователю("Нужно удалить в загружаемом файле - первые, пустые строки!");
		Возврат;
		
	КонецПопытки;	
	
	Если ТаблицаСчетов.Количество() = 0 Тогда
		
		ОбщегоНазначения.СообщитьИнформациюПользователю("Нет данных для загрузки, обработка прекращена!");
		Возврат;
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВнешняяТаблица", ТаблицаСчетов);
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаСчетов.КодБыстрогоВыбора,
		|	ТаблицаСчетов.НаименованиеАнгл
		|ПОМЕСТИТЬ ВТ_ТаблицаСчетов
		|ИЗ
		|	&ВнешняяТаблица КАК ТаблицаСчетов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Хозрасчетный.Ссылка,
		|	ВТ_ТаблицаСчетов.НаименованиеАнгл
		|ИЗ
		|	ВТ_ТаблицаСчетов КАК ВТ_ТаблицаСчетов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный КАК Хозрасчетный
		|		ПО ВТ_ТаблицаСчетов.КодБыстрогоВыбора = Хозрасчетный.КодБыстрогоВыбора";
		
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		СчетОбъект = Выборка.Ссылка.ПолучитьОбъект();
		СчетОбъект.НаименованиеАнгл = Выборка.НаименованиеАнгл; 
		
		Попытка
			СчетОбъект.Записать();
		Исключение
			
			ОбщегоНазначения.СообщитьИнформациюПользователю("Не удалось записать счет, " + Выборка.Ссылка + "
															|по причине: " + ОписаниеОшибки());
			Возврат;
			
		КонецПопытки;
		
	КонецЦикла;
	
	ОбщегоНазначения.СообщитьИнформациюПользователю("Изменены наименования - " + Выборка.Количество() + " счетов!");
	
КонецПроцедуры
